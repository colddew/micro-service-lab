buildscript {

    ext {
        springBootVersion = "2.1.4.RELEASE"
        dependencyManagementVersion = "1.0.7.RELEASE"
        gradleDockerVersion = "0.22.0"
        retrofitVersion = "2.5.0"
        findbugsVersion = "jsr305:2.0.1"
        springJaegerVersion = "1.0.3"
    }

    repositories {
        maven { url 'http://localhost:8081/repository/maven-public/'}
        maven { url "https://plugins.gradle.org/m2/" }
//        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:${gradleDockerVersion}"
    }
}

allprojects {

    group 'cn.plantlink'
    version '1.0.3-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven { url 'http://localhost:8081/repository/maven-public/'}
//        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

subprojects {

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.palantir.docker'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-actuator'
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
        compile "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
        compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
        compile "com.google.code.findbugs:${findbugsVersion}"
        compile "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:${springJaegerVersion}"
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    docker {
        name "${project.group}/${jar.baseName}:${jar.version}"
        files jar.archivePath
        buildArgs(['JAR_FILE': "${jar.archiveName}"])
    }

    publishing {

        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }

        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url = "http://localhost:8081/repository/maven-snapshots/"
                } else {
                    url = "http://localhost:8081/repository/maven-releases/"
                }
                credentials {
                    username 'admin'
                    password 'admin123'
                }
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
//        html.destination file("${buildDir}/reports/jacoco")
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}